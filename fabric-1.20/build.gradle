plugins {
    id 'java'
    id 'idea'
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

base {
    version = '2.0.1-fabric'
    group = 'io.github.hexagonnico.spidercaves'
    archivesName = "spidercaves-1.20"
}

java {
    toolchain {
        JavaLanguageVersion.of(17)
    }
}

dependencies {
    minecraft "com.mojang:minecraft:1.20.1"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.14.23"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.90.4+1.20.1"
    implementation project(":common-1.20")
    implementation project(":common")
}

loom {
    mixin {
        defaultRefmapName.set "mixin.spidercaves.refmap.json"
    }
    runs {
        client {
            client()
            setConfigName("fabric-1.20")
            ideConfigGenerated(true)
            runDir("../run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source project(":common").sourceSets.main.allSource
    source project(":common-1.20").sourceSets.main.allSource
}

processResources {
    from project(":common").sourceSets.main.resources
    from project(":common-1.20").sourceSets.main.resources
}

jar {
    manifest {
        attributes([
            "Specification-Title": 'spider_caves',
            "Specification-Vendor": 'HexagonNico',
            "Specification-Version": project.version,
            "Implementation-Title": project.name,
            "Implementation-Version": project.jar.archiveVersion,
            "Implementation-Vendor": 'HexagonNico',
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
